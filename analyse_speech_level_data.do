/*
	analyse_speech_level_data.do
	
	> Reads in combined speech-level data (generated by "combine_speech_level_data.py").
	
	> Creates time trends of speech partisanship in "God" speeches and other speeches (Figure 1).
	
	> Runs panel regressions which compare speech partisanship with use of the word God (Table 1).


*/

clear all
set more off
cls

grstyle init
grstyle set plain, horizontal grid

cd "C:\Users\Anthony\Documents\Projects\rtalk-final2\data"

// Read in combined speech-level data //

use "speech_data.dta", replace

rename (republican democrat) (rep dem)

*generate transformation of speech partisanship ("biast") so that it always increases as a speaker sounds more like 
*their affiliated party
g biast = . 
replace biast = bias if rep==1
replace biast = -bias if dem==1


// Create time trend of speech partisanship in "God" speeches and other speeches (Figure 1) //

preserve
collapse (mean) biast (semean) biast_se = biast, by(god decade)
g biast_upper = biast + 1.96*biast_se
g biast_lower = biast - 1.96*biast_se
twoway (rarea biast_upper biast_lower decade if god_dummy==1, color(gs12)) ///
		(rarea biast_upper biast_lower decade if god_dummy==0, color(gs12)) ///
		(line biast decade if god_dummy==1, color(forest_green) lwidth(vthin)) ///
		(line biast decade if god_dummy==0, color(orange) lwidth(vthin)),  ///
		ytitle("Partisanship") xtitle("Decade") legend(order(3 `""God" Speeches"' 4 "Rest of Corpus")) name("partisan_trend", replace)
graph export partisan_trend.eps, name(partisan_trend) replace
restore

// Run panel regressions which compare speech partisanship with use of the word God (Table 1) //

*generate speaker FE
egen speakerfe = group(last start)

*standardise "biast"
egen zbiast = std(biast)

*full sample regression
reghdfe god_dummy zbiast, absorb(session speakerfe) cluster(speakerfe)
est tab, p(%9.3g)

*regression with sample of Republican speakers
reghdfe god_dummy zbiast if rep==1, absorb(session speakerfe) cluster(speakerfe)
est tab, p(%9.3g)

*regression with sample of Democrat speakers
reghdfe god_dummy zbiast if dem==1, absorb(session speakerfe) cluster(speakerfe)
est tab, p(%9.3g)






